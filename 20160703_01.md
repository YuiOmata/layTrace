# Processingでレイトレーシング！

第一章: レイキャスト・その１  

小渕 豊 ( @FMS_Cat )  
Light Transport Entertainment Inc.  

# 最終目標

最終的には、我々は絵を描きたい  
→ **絵を構成する各ピクセルに対応する色を知りたい**  

```Processing
void setup() {
  size(256, 256);

  for (int iy = 0; iy < height; iy ++) {
    for (int ix = 0; ix < width; ix ++) {
      stroke(calcPixelColor(ix, iy));
      point(ix, iy);
    }
  }
}
```

# 試しに

グラデーションを描いてみる  

```Processing
color calcPixelColor(int x, int y) {
  int red = int(x * 1.0 / width * 256.0);
  int green = int(y * 1.0 / height * 256.0);
  return color(
    red,
    green,
    0
  );
}
```

![](https://i.imgur.com/fd4gPDH.png)

# 今回の目標

球体シルエットの描画  

![](https://i.imgur.com/OGeGweq.png)

# レイキャスト？

ある点からある方向に半直線を飛ばし、半直線がモノと交差するかを調べる  

![](https://i.imgur.com/HDe6ZUJ.png)  

# レイ

半直線を **レイ** と呼ぶ  
レイは **始点** と **方向** の情報を持つ  

🤔 < でもどんな情報だ…？  

# ベクトル

**ベクトル** : 大きさと向きを持った量  
矢印のメタファで表わされることが多い  

![](https://i.imgur.com/KJG0p7l.png)  

3次元空間では、x, y, zの3成分を持つ  

# Vec

`Vec` （ベクトル）クラスをあらかじめ用意してあります  

## メンバ変数  

- `float x`  
- `float y`  
- `float z`  

## メンバメソッド  

- `String toString()` : `"Vec(1.0, 2.0, 3.0)"` のように文字列を出力  
- `Vec add(Vec)` : ベクトル同士の加算  
- `Vec sub(Vec)` : ベクトル同士の減算  
- `Vec scale(float)` : ベクトルの定数倍  
- `Vec neg()` : 逆向きのベクトルを返す  
- `float len()` : ベクトルの長さを返す  
- `Vec normalize()` : 正規化ベクトルを返す（長さが1になる）  
- `float dot(Vec)` : 内積  
- `Vec cross(Vec)` : 外積

❓ < ところで、クラスってみんなわかる？

# Vecであそぼう

```
void setup() {
  Vec vec = new Vec(1.0, 2.0, 3.0); // ベクトルを作る
  println(vec); // ベクトルをConsoleに出力

  vec = vec.add(new Vec(5.0, 1.0, 3.0)); // ベクトル同士の足し算
  println(vec); // 足した後のベクトルをConsoleに出力

  println(vec.len()); // ベクトルの長さをConsoleに出力
}
```

# Ray

`Vec` ができれば `Ray` （レイ）も作れる！  
Remember: レイは半直線、 **始点** と **方向** の情報を持つ  

## メンバ変数

- `Vec origin` : 始点  
- `Vec dir` : 方向  

# 最初の目標

**レイを飛ばした先に物があるか調べたい**  

```Processing
boolean isIntersect(Ray ray) {
  ...
}
```

# シーン

ベクトルとレイはできた…  
でも何もない空間でレイを飛ばしても、何も起こらない…！！！  

→ **モノ** と **空間** を定義しよう  

# Intersectable

**モノ** は `Intersectable` として定義します  

```Processing
interface Intersectable {
  boolean isIntersect(Ray ray);
}
```

つまり:  
モノがモノであるために、  
`Ray` を投げると `boolean` を返す `isIntersect`  
というメソッドを実装してね  

# Sphere

試しに **球** を `Intersectable` として実装してみよう  

## メンバ変数

- `Vec center` : 球の中心座標  
- `float radius` : 球の半径  

## メンバメソッド

- `isIntersect`

# 球と半直線の交差判定

レイと球の位置関係から連立方程式を立てて解を調べる  
円と半直線の交差判定の３次元版  

**我々が式を求める必要はない！**  
我々が知っているべきことは「使える式がある」ということ  
式自体はググればすぐ出る

# isIntersect (Sphere)

```Processing
class Sphere implements Intersectable {
  boolean isIntersect(Ray ray) {
    Vec v = ray.origin.sub(center);
    float b = ray.dir.dot(v);
    float c = v.dot(v) - sq(radius);
    float d = b * b - c;
    if (d >= 0) {
      float s = sqrt(d);
      float t = -b - s;
      if (t <= 0)  t = -b + s;

      if (0 < t) {
        return true;
      }
    }
    return false;
  }
}
```

# Scene

**空間** は `Scene` として定義します  

```Processing
class Scene {
  ...
}
```

# ObjList

まず、空間はモノの集合を持つ必要がある  

```Processing
class Scene {
  ArrayList<Intersectable> objList = new ArrayList<Intersectable>();

  void add(Intersectable obj) {
    objList.add(obj);
  }
}
```

# isIntersect (Scene)

空間内の任意のモノと交差すれば `true` を返す  

```
class Scene {
  boolean isIntersect(Ray ray) {
    for (int i = 0; i < objList.size(); i ++) {
      Intersectable obj = (Intersectable) objList.get(i);
      if (obj.isIntersect(ray)) {
        return true;
      }
    }
    return false;
  }
}
```

# とりあえずレイを飛ばしてみる

```
scene = new Scene();

Vec sphereCenter = new Vec(0.0, 0.0, 0.0);
float sphereR = 1.0;
Sphere sphere = new Sphere(sphereCenter, sphereR);
scene.add(sphere);

Vec rayOrigin = new Vec(0.0, 0.0, 5.0);
Vec rayDir = new Vec(0.0, 0.0, -1.0).normalize();
Ray ray = new Ray(rayOrigin, rayDir);

println(scene.isIntersect(ray));
```

`true` が返ってくればOK！  
`rayDir` や `rayOrigin` などをいじって遊んでみよう  

# 描画したい……

レイは飛ばせたが、描画はできていない…  

Remember: 最終的に我々は、 絵を構成する各ピクセルに対応する色を知りたい  
→ **各ピクセルに対応するベクトルを知りたい**  
→ カメラを作ろう  

# Camera

`Camera` （カメラ）クラスを用意してあります  

## 使い方

- `void camera.lookat(...)` という関数で初期設定をする  
- `void camera.fov(fov)` という関数で視野角を設定できる  
- `Ray camera.ray(x, y)` でピクセルの座標に対応したベクトルが取り出せる  

## lookat

```
camera.lookat(
  eye, // カメラの位置
  target, // カメラの標的
  up, // ほとんどの場合、 Vec(0.0, 1.0, 0.0) でOK
)
```

# 描画！

```
void setup() {
  scene = initScene();

  Vec sphereCenter = new Vec(0.0, 0.0, 0.0);
  float sphereR = 1.0;
  Sphere sphere = new Sphere(sphereCenter, sphereR);
  scene.add(sphere);

  camera = new Camera();
}
```

```
Scene initScene() {
  Scene scene = new Scene();

  Vec sphereCenter = new Vec(0.0, 0.0, 0.0);
  float sphereR = 1.0;
  Sphere sphere = new Sphere(sphereCenter, sphereR);
  scene.add(sphere);

  return scene;
}
```

```
color calcPixelColor(int x, int y) {
  Ray ray = camera.ray(x, y);

  if (scene.isIntersect(ray)) {
    return color(255);
  } else {
    return color(0);
  }
}
```

# 完成！

![](https://i.imgur.com/OGeGweq.png)

# モノはたくさん出せます

```
Scene initScene() {
  ...

  for (int i = 0; i < 5; i ++) {
    Vec sphereCenter = new Vec(i - 2, 0.0, 0.0);
    float sphereR = 0.5;
    Sphere sphere = new Sphere(sphereCenter, sphereR);
    scene.add(sphere);
  }

  ...
}
```
