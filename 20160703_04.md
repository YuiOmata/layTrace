# Processingでレイトレーシング！

第四章: パストレース  

小渕 豊 ( @FMS_Cat )  
Light Transport Entertainment Inc.  

# 今回の目標

ヤバイ  

![](https://i.imgur.com/9Mk1pMX.png)  

# 陰影

「赤い物体」も、すべての位置の表面が等しい赤ではない  
→ 光源や他の物体との位置関係により、陰影 (Shade) や影 (Shadow) ができる  

![](https://i.imgur.com/tfTfFwQ.png)

# 光源と物体

本来現実世界では、レイはシーン（光源）からカメラに入ってくるはず  
光源から撒き散らされたレイがいろいろな物体に反射し、偶然カメラに入ってきたもの（激レア）が像として映される  

![](https://i.imgur.com/yxrJXAO.png)

# レイトレではレイは逆向き

（今更ですが、）レイトレーシングにおいて、レイはカメラからシーンに飛ばす  
→ 現実世界と真逆！  

カメラに入ってこないレイは（カメラにとっては）あまり意味が無い  
→ 計算が無駄になる

# 拡散反射

拡散反射: でたらめな方向に光が反射する  
物体の表面は我々の想像以上にざらざらしている（レイ談）  
世の中はギラギラ鏡のように反射するモノばかりではない  

![](https://i.imgur.com/hxNbnuB.png)

# ここまでの物体表面

![](https://i.imgur.com/K12llFQ.png)

# パストレースにおける物体表面

![](https://i.imgur.com/0MLAC0O.png)

# パストレース

拡散反射する物体にレイが衝突した場合、 **ランダムな方向にレイを飛ばす**  

![](https://i.imgur.com/TwWS8TV.png)  

🤔 < でもそんなことしたら、でたらめな結果になっちゃうのでは……？  

# モンテカルロ法

**ランダムな試行は徐々に確率に支配される**  

例えば、コインを100回投げることを考える  
→ 表が出る回数は50回になりそう  
→ 表が出る確率は0.5になりそう  

100回の時より1000回、1000回の時より10000回の時のほうが確率は0.5になりそう  
→ 試行回数を増やせば増やすほど確率は正確になる  

# モンテカルロ法とパストレーシング

「ランダムな方向に光を飛ばす」を何回も繰り返せば、やがて正しい結果に近づいていく  
→ あとで試行回数をいじりながら確認しましょう  

たくさんレイを飛ばして、色の平均を出しましょう  

# randomDirOverHemisphere

`Vec` に半球のうちからランダムな方向を返すメソッドを追加

```Processing
class Vec {
  Vec randomDirOverHemisphere() {
    Vec v = new Vec(0.0, 0.0, 0.0);

    while (true) {
      v.x = random(-1.0, 1.0);
      v.y = random(-1.0, 1.0);
      v.z = random(-1.0, 1.0);
      float l = v.len();
      if (0.0 < l && l < 1.0) { break; }
    }

    v = v.normalize();
    if (v.dot(this) < 0.0)  v = v.neg();
    return v;
  }
}
```

# trace改造

`Scene.trace` を改造します  
とりあえず鏡面反射のことは忘れよう  

```Processing
class Scene {
  Spectrum trace(Ray ray, int depth) {
    ...

    if (isect.t != NO_HIT) {
      return traceDiffuse(ray, isect, depth);
    } else {
      return BLACK;
    }
  }
}
```

# traceDiffuse

```Processing
class Scene {
  Spectrum traceDiffuse(Ray ray, Intersection isect, int depth) {
    Vec dir = isect.n.randomDirOverHemisphere();
    Ray ray2 = new Ray(isect.p, dir);
    ray2.forwardEpsilon();
    Spectrum li = trace(ray2, depth + 1);

    Spectrum fr = isect.material.col.scale(1.0 / PI);
    float sc = 2.0 * PI * isect.n.dot(dir);

    return li.mul(fr).scale(sc);
  }
}
```

`li` : `trace` （再帰）  
`fr` : BRDF（追跡した方向から入射する光が元のレイの方向にどのくらい反射されるか）  
`sc` : 係数  

# あれ？

![](https://i.imgur.com/Yja6aUm.png)

超黒い

# 光源がない

レイがどう反射しようと、BLACKにしか終着しない…  
→ 空の色を黒じゃなくしよう  

# 空の色

```
Spectrum SKY = new Spectrum(0.9, 0.9, 0.9);
```

```
class Scene {
  Spectrum trace(Ray ray, int depth) {
    ...
    if (isect.t != NO_HIT) {
      ...
    } else {
      return SKY;
    }
  }
}
```

# 出てきた

![](https://i.imgur.com/Awl0jvs.png)  

でもとてもノイジー

# 平均を計算

Remember: モンテカルロ法「たくさんレイを飛ばして、色の平均を出しましょう」  

```
color calcPixelColor(int x, int y) {
  Ray ray = camera.ray(x, y);

  Spectrum sum = new Spectrum(0.0, 0.0, 0.0);

  for (int i = 0; i < SAMPLES; i ++) {
    sum = sum.add(scene.trace(ray, 0).scale(1.0 / SAMPLES));
  }

  return sum.toColor();
}
```

# どうだ

![](https://i.imgur.com/9Mk1pMX.png)

# 最後に、鏡面反射も

拡散反射するか、鏡面反射にするかも確率で決めてしまおう  

```
class Scene {
  Spectrum trace(Ray ray, int depth) {
    ...

    if (isect.t != NO_HIT) {
      float dice = random(1.0);
      if (dice < isect.material.reflective) {
        return traceReflect(ray, isect, depth);
      } else {
        return traceDiffuse(ray, isect, depth);
      }
    } else {
      return SKY;
    }
  }
}
```

# 完成！

![](https://i.imgur.com/EBsyF2Q.png)
