# Processingでレイトレーシング！

第二章: レイキャスト・その２  

小渕 豊 ( @FMS_Cat )  
Light Transport Entertainment Inc.  

# 今回の目標

色のついた球体の描画  

![](https://i.imgur.com/6IDOOhr.png)  

# 色

> 光は様々な波長の電磁波が重ね合わされてできています。 そしてその波長の違いを識別する事によって私たちは脳で色を感じているのです。
>
> このような光(電磁波)の波長や帯域のことを スペクトル(Spectrum) といいます。 つまり「色」はスペクトルによって表現することができます。

色は様々なスペクトルの組み合わせで表現できる  
今回は 赤・緑・青 の3波長を用います  

# Spectrum

スペクトルを扱うため、 `Spectrum` クラスを導入  

## メンバ変数  

- `float r` : スペクトルの赤成分  
- `float g` : スペクトルの緑成分  
- `float b` : スペクトルの青成分  

## メンバメソッド  

- `Spectrum add(Spectrum)` : スペクトルの足しあわせ  
- `Spectrum mul(Spectrum)` : スペクトルの掛けあわせ  
- `Spectrum scale(Spectrum)` : スペクトルの定数倍  
- `color toColor()` : スペクトルを色に変換  

# BLACK

```Processing
Spectrum BLACK = new Spectrum(0, 0, 0);
```

# マテリアル

物体の見え方は、材質によって異なる  
e.g. 色、光沢感、透明感……  

# Material

材質を扱うため、 `Material` クラスを導入  
今回は色（ `col` ）しか扱わないが、将来的に拡張していく  

## メンバ変数

- `float col` : 材質の色  

# 現在の `scene.isIntersect` の問題点

とりあえず、レイがモノと交差したら `true` を返す  
→ すべてのモノを同一のモノとして扱ってしまっている  
→ **モノごとに色分けができない！**  

# 交差をよりスマートに扱う

現在 `scene.isIntersect` は、物体と衝突したか否かの情報を持つ `boolean` しか返さない  
より詳細な情報を返すようにしよう  

# Intersection

交差の情報を扱うクラス `Intersection` を導入  

## メンバ変数

- `float t` : 交差点までの距離
- `Vec p` : 交差点  
- `Vec n` : 法線  
- `Material material` : 交差したモノのマテリアル  

🤔 < 法線……？

# 法線  

物体表面の向き  
ベクトルで表現できる  

![](https://i.imgur.com/hQxe3hF.png)  

# 衝突したか？

今まで `boolean` で返していたとおり、 `Intersection` にも、物体に衝突したかの情報は必要となる  
→ `t` （交差点までの距離） で対処

```Processing
float NO_HIT = Float.POSITIVE_INFINITY;
```

`t` が無限大（ = `NO_HIT` ）の場合は衝突していないものとする  

# intersect (Intersectable)

```Processing
interface Intersectable {
  Intersection intersect(Ray ray);
}
```

# intersect (Sphere)

```Processing
Intersection intersect(Ray ray) {
  Intersection isect = new Intersection();

  Vec v = ray.origin.sub(center);
  float b = ray.dir.dot(v);
  float c = v.dot(v) - sq(radius);
  float d = b * b - c;
  if (d >= 0) {
    float s = sqrt(d);
    float t = -b - s;
    if (t <= 0)  t = -b + s;

    if (0 < t) {
      isect.t = t;
      isect.p = ray.origin.add(ray.dir.scale(t));
      isect.n = isect.p.sub(center).normalize();
      isect.material = material;
    }
  }
  return isect;
}
```

# おっと

`Sphere` に `material` を登録しなければ

```Processing
class Sphere implements Intersectable {
  Vec center;
  float radius;
  Material material;

  Sphere(Vec center, float radius, Material material) {
    this.center = center;
    this.radius = radius;
    this.material = material;
  }

  ...
}
```

# intersect (Scene)

Important: **一番近い物体を返す**  

```Processing
class Scene {
  Intersection intersect(Ray ray) {
    Intersection isect = new Intersection();
    for (int i = 0; i < objList.size(); i ++) {
      Intersectable obj = (Intersectable) objList.get(i);
      Intersection objIsect = obj.intersect(ray);
      if (objIsect.t < isect.t) {
        isect = objIsect;
      }
    }
    return isect;
  }
}
```

# でも正直

> ぼく「このレイ飛ばしたらどんなモノに当たるか教えて」  
> シーン「こんな距離、こんな座標、こんな法線で、こんな材質の物体にあたります」  

より

> ぼく「このレイ飛ばしたらなに色になるか教えて」  
> シーン「赤やで」  

のほうがわかりやすい

# trace

```Processing
class Scene {
  Spectrum trace(Ray ray) {
    Intersection isect = scene.intersect(ray);
    if (isect.t != NO_HIT) {
      return isect.material.col;
    } else {
      return BLACK;
    }
  }
}
```

# やってみよう

赤〜黄色の球体を5つ出してみる

```Processing
Scene initScene() {
  ...

  for (int i = 0; i < 5; i ++) {
    Vec sphereCenter = new Vec(i - 2, 0.0, i - 2);
    float sphereR = 0.7;
    Material sphereMaterial = new Material(new Spectrum(1.0, i * 0.25, 0.0));
    Sphere sphere = new Sphere(sphereCenter, sphereR, sphereMaterial);
    scene.add(sphere);
  }

  ...
}
```

`calcPixelColor` はこれだけ！

```Processing
color calcPixelColor(int x, int y) {
  Ray ray = camera.ray(x, y);
  return scene.trace(ray).toColor();
}
```

# 完成！

![](https://i.imgur.com/6IDOOhr.png)  
